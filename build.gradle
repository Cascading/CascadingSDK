/*
 * Copyright (c) 2007-2013 Concurrent, Inc. All Rights Reserved.
 *
 * Project and contact information: http://www.concurrentinc.com/
 */

import org.apache.tools.ant.filters.ReplaceTokens

import java.text.SimpleDateFormat

if( project.properties[ 'teamcity' ] ) // make them system properties
  System.properties.putAll( project.properties[ 'teamcity' ] )

if( System.properties[ 'aws.properties' ] )
{
  file( System.properties[ 'aws.properties' ] ).withReader { reader ->
    def awsProperties = new Properties()
    awsProperties.load( reader )
    System.properties.putAll( awsProperties )
  }
}

ext.majorVersion = 2.2
ext.applicationGroup = "sdk"
ext.applicationName = "Cascading-${majorVersion}-SDK"
ext.timestamp = new SimpleDateFormat( "yyyyMMdd" ).format( new Date() )
ext.archiveBaseName = "${applicationName}-${timestamp}"
ext.distDir = "${buildDir}/dist/${archiveBaseName}"
ext.distributionDir = "${buildDir}/distribution"

ext.sourceBucket = "files.concurrentinc.com"
ext.docSourceBucket = "docs.concurrentinc.com"

configurations {
  s3AntTask
}

dependencies {
  s3AntTask 'thirdparty:awstasks:0.3'
}

allprojects {
  repositories {
    mavenLocal()
    mavenCentral()
    mavenRepo name: 'conjars', url: 'http://conjars.org/repo/'
  }
}

task copyEnv( type: Copy ) {
  from 'etc/setenv.sh'
  into "${distDir}/etc"
}

copyEnv.doFirst() {
  mkdir "${distDir}/etc"

  def binPaths = [ ]

  fileTree( distDir ) {
    include '*/*/bin/'
  }.visit { element ->
    if( element.isDirectory() && element.getName().equals( 'bin' ) )
      binPaths << element.getRelativePath()
  }

  def pathString = binPaths.collect { path -> "\${CASCADING_SDK_HOME}/${path}" }.join( ':' )

  filter( ReplaceTokens, tokens: [ BIN_PATHS: pathString ] )
}

task copySupport( type: Copy, dependsOn: copyEnv ) {
  from 'README.md'
  into distDir
}

task packageDist( type: Tar, dependsOn: copySupport ) {
  description = 'Creates the sdk archive.'

  baseName = applicationName
  version = timestamp
  compression = "GZIP"
  destinationDir = file( distributionDir )
  from "${buildDir}/dist/"
}

allprojects {
  task clean( type: Delete ) {
    delete buildDir
  }
}

subprojects {

  ext.distDir = "${buildDir}/dist"

  task fetchLatest {
    ext.latestURL = null
    ext.distroURL = null
    ext.typePath = ''

    outputs.dir distDir
  }

  fetchLatest.onlyIf { fetchLatest.latestURL != null || fetchLatest.distroURL != null }

  fetchLatest << {

    mkdir buildDir
    mkdir distDir

    if( distroURL == null )
    {
      def latestFile = new File( buildDir, 'LATEST.TXT' )

      ant.get( src: latestURL, dest: latestFile.absolutePath, skipexisting: false )

      distroURL = latestFile.readLines()[ 0 ]
    }

    def distroZip = new File( buildDir, distroURL.substring( distroURL.lastIndexOf( '/' ) ) )

    ant.get( src: distroURL, dest: distroZip.absolutePath, skipexisting: true )

    ext.typeDir = new File( distDir, typePath )
    mkdir typeDir

    if( distroURL.endsWith( 'zip' ) || distroURL.contains( 'zipball' ) )
      ant.unzip( src: distroZip, dest: typeDir )
    else
      ant.untar( src: distroZip, dest: typeDir, compression: 'gzip' )
  }

  task distCopy( type: Copy, dependsOn: fetchLatest ) {
    from( distDir )
    into( rootProject.distDir )

    rootProject.tasks[ 'packageDist' ].dependsOn << distCopy
    rootProject.tasks[ 'copyEnv' ].dependsOn << distCopy
  }
}

// publishing code

task createArchiveLatest( dependsOn: packageDist ) {

  ext.latestArchivePath = null
}

createArchiveLatest << {

  def publishBucket = System.properties[ 'publish.bucket' ]
  def releaseTar = packageDist.archiveName

  latestArchivePath = new File( buildDir, 'latest.txt' )
  latestArchivePath.write( "http://${publishBucket}/${applicationGroup}/${majorVersion}/${releaseTar}" )
}

task s3Upload( dependsOn: createArchiveLatest ) << {

  def awsAccessId = System.properties[ 'publish.aws.accessId' ]
  def awsSecretKey = System.properties[ 'publish.aws.secretKey' ]
  def s3Bucket = System.properties[ 'publish.bucket' ]

  assert s3Bucket

  def remotePath = "${applicationGroup}/${majorVersion}/"

  ant.taskdef( name: 's3Upload', classname: 'dak.ant.taskdefs.S3Upload',
          classpath: configurations.s3AntTask.asPath )

  ant.s3Upload( verbose: 'true', accessId: awsAccessId, secretKey: awsSecretKey,
          bucket: s3Bucket, prefix: remotePath, publicRead: 'true' ) {

    fileset( file: packageDist.archivePath )
    fileset( file: createArchiveLatest.latestArchivePath )
    fileset( file: 'etc/emr/install-cascading-sdk.sh' )
  }
}


